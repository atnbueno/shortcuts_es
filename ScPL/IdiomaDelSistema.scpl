@Icon puzzle
@Color darkgray

@set scTitle "Idioma del sistema"
@set scVersion "1.0.1"
@set scURL "https://www.atnbueno.com/atajos/IdiomaDelSistema"
@set scLanguage es
@set scRoutineHubID 3006

Comment
| Atajos 2.2.1 está traducido a 39 idiomas o dialectos. Este atajo detecta cual de dichas traducciones se está usando (excepto “en_AU” y “en_GB”, que se detectan como “en”). — @atnbueno (2019-06-18)
|
| P.S. Probado con éxito en “ar”, “ca”, “de”, “en”, “es”, “es_419”, y “sv”
|
| TITLE=\(@:scTitle)
| VERSION=\(@:scVersion)
| URL=\(@:scURL)
| LANGUAGE=\(@:scLanguage)
|
| RoutineHubUpdate
|
| Current Version: "\(@:scVersion)"
| RoutineHub ID: "\(@:scRoutineHubID)"
|
| RoutineHubUpdate
Count Items
If input="Is Greater Than" number=0
	Comment "Modo función externa"
	Text "email@example.com"
	GetEmailAddressesfromInput
	GetType
	SetVariable DataType
	If input=Equals value="Endereço de e‑mail"
		SetName name="example.rtf"
		GetType
		SetVariable DataType
		Dictionary {"Texto com estilo": pt, "Texto formatado": pt_PT}
	Otherwise
		Dictionary {"عنوان بريد إلكتروني": ar, "Adreça de correu": ca, "E‑mailová adresa": cs, "E-mailadresse": da, "E-Mail-Adresse": de, "Διεύθυνση email": el, "Email address": en, "Dirección de correo electrónico": es, "Dirección de correo": es_419, "Sähköpostiosoite": fi, "Adresse e-mail": fr, "Adresse courriel": fr_CA, "כתובת דוא״ל": he, "ईमेल पता": hi, "e-mail adresa": hr, "E-mail cím": hu, "Alamat email": id, "Indirizzo e-mail": it, "メールアドレス": ja, "이메일 주소": ko, "Alamat e-mel": ms, "E-mailadres": nl, "E-postadresse": no, "Adres email": pl, "Adresă de e-mail": ro, "Адрес e-mail": ru, "Emailová adresa": sk, "E-postadress": sv, "ที่อยู่อีเมล": th, "E-posta adresi": tr, "Адреса е-пошти": uk, "Địa chỉ email": vi, "电子邮件地址": zh_CN, "電郵地址": zh_HK, "電子郵件位址": zh_TW}
	End
	GetDictionaryValue key=v:DataType
Otherwise
	Comment "Modo demo"
	RunShortcut shortcut=@:scTitle showWhileRunning=false -> mv:ShortcutOutput
	ShowAlert title="\(@:scTitle) (modo demo)" message="\nIdioma del sistema detectado: \(mv:ShortcutOutput)\n\nUsa este atajo como función externa\nejecutándolo con cualquier entrada" showCancelButton=false
End